; ***************************************************************************************
; ***************************************************************************************
;
;		Name : 		unary.def
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		21st October 2021
;		Purpose :	Unary operators (A ? B -> A)
;
; ***************************************************************************************
; ***************************************************************************************

@copies		---
		dec 	hl
		dec 	hl

; ***************************************************************************************

@copies 	--
		dec 	hl

; ***************************************************************************************

@copies 	++
		inc 	hl

; ***************************************************************************************

@copies		+++
		inc 	hl
		inc 	hl

; ***************************************************************************************

@calls 	0<
		bit 	7,h
		ld 		hl,$0000
		ret 	z
		dec 	hl
		ret

; ***************************************************************************************

@calls 	0=
		ld 		a,h
		or 		l
		ld 		hl,$0000
		ret 	nz
		dec 	hl
		ret

; ***************************************************************************************

@copies	2* 
		add 	hl,hl

@copies	4* 
		add 	hl,hl
		add 	hl,hl

@copies	8* 
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl

@copies	16*
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl

; ***************************************************************************************

@copies	2/ 
		sra 	h
		rr 		l

@calls	4/
		sra 	h
		rr 		l
		sra 	h
		rr 		l
		ret

@calls	8/
		sra 	h
		rr 		l
		sra 	h
		rr 		l
		sra 	h
		rr 		l
		ret

@calls	16/
		sra 	h
		rr 		l
		sra 	h
		rr 		l
		sra 	h
		rr 		l
		sra 	h
		rr 		l
		ret

; ***************************************************************************************

@calls 	abs
		bit 	7,h
		ret		z
		jr 		__negate

; ***************************************************************************************

@calls 	0-
__negate:
		ld 		a,h
		cpl 
		ld 		h,a
		ld 		a,l
		cpl
		ld 		l,a
		inc 	hl
		ret

; ***************************************************************************************

@copies	bswap	
		ld 		a,l
		ld 		l,h
		ld 		h,a

; ***************************************************************************************

@calls 	not
		ld 		a,h
		cpl 
		ld 		h,a
		ld 		a,l
		cpl
		ld 		l,a
		ret

; ***************************************************************************************

@calls strlen
		push 	de
		ex 		de,hl
		ld 		hl,0
_SLNLoop:
		ld 		a,(de)
		or 		a
		jr 		z,_SLNExit
		inc 	de
		inc 	hl
		jr 		_SLNLoop
_SLNExit:
		pop 	de
		ret
				
; ***************************************************************************************

@calls 	random
	ex 		de,hl
	push 	bc
    ld 		hl,(seed1)
    ld 		b,h
    ld 		c,l
    add 	hl,hl
    add 	hl,hl
    inc 	l
    add 	hl,bc
    ld 		(seed1),hl
    ld 		hl,(seed2)
    add 	hl,hl
    sbc 	a,a
    and 	%00101101
    xor 	l
    ld 		l,a
    ld 		(seed2),hl
    add 	hl,bc
    pop 	bc
    ret

